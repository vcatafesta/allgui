@echo off
rem
rem $Id: BuildApp.bat $
rem

:BUILDAPP

   cls

   if /I not "%1" == "/C" goto ROOT
   shift
   set HG_ROOT=
   set HG_HRB=
   set HG_MINGW=
   set HG_BCC=
   set HG_CCOMP=
   set LIB_GUI=
   set LIB_HRB=
   set BIN_HRB=

:ROOT

   if not "%HG_ROOT%" == "" goto TEST

   set THIS_DRIVE_AND_PATH=%~dp0
   set HG_ROOT=%THIS_DRIVE_AND_PATH%
   if "%THIS_DRIVE_AND_PATH:~-1%" == "\" set HG_ROOT=%THIS_DRIVE_AND_PATH:~0,-1%
   set THIS_DRIVE_AND_PATH=

:TEST

   if /I "%1" == "HB30" goto TEST_HB30
   if /I "%1" == "HB32" goto TEST_HB32

:DETECT_HB30

   if exist "%HG_ROOT%\BuildApp30.bat" goto DETECT_HB32
   if exist "%HG_ROOT%\BuildApp32.bat" goto COMPILE_HB32
   echo File %HG_ROOT%\BuildApp30.bat not found !!!
   echo File %HG_ROOT%\BuildApp32.bat not found !!!
   echo.
   goto END

:DETECT_HB32

   if not exist "%HG_ROOT%\BuildApp32.bat" goto COMPILE_HB30
   echo Syntax:
   echo    To build with Harbour 3.0
   echo       BuildApp [/C] HB30 file [options]
   echo    To build with Harbour 3.2
   echo       BuildApp [/C] HB32 file [options]
   echo.
   echo    Options:
   echo       f        to redirect all output to error.log
   echo       -rebuild to force compiling of all sources
   echo       -trace   to see the commands generated by hbmk2
   echo       -info    to see informational messages
   echo       other hbmk2 options
   echo.
   goto END

:TEST_HB30

   shift
   if exist "%HG_ROOT%\BuildApp30.bat" goto COMPILE_HB30
   echo File %HG_ROOT%\BuildApp30.bat not found !!!
   echo.
   goto END

:TEST_HB32

   shift
   if exist "%HG_ROOT%\BuildApp32.bat" goto COMPILE_HB32
   echo File %HG_ROOT%\BuildApp32.bat not found !!!
   echo.
   goto END

:COMPILE_HB30

   if "%HG_HRB%"   == "" set HG_HRB=%HG_ROOT%\hb30
   if "%HG_MINGW%" == "" set HG_MINGW=%HG_CCOMP%
   if "%HG_MINGW%" == "" set HG_MINGW=%HG_HRB%\comp\mingw
   if "%HG_CCOMP%" == "" set HG_CCOMP=%HG_MINGW%
   if "%LIB_GUI%"  == "" set LIB_GUI=lib
   if "%LIB_HRB%"  == "" set LIB_HRB=lib
   if "%BIN_HRB%"  == "" set BIN_HRB=bin
   call "%HG_ROOT%\BuildApp_hbmk2.bat" %1 %2 %3 %4 %5 %6 %7 %8 %9
   goto END

:COMPILE_HB32

   if "%HG_HRB%"   == "" set HG_HRB=%HG_ROOT%\hb32
   if "%HG_MINGW%" == "" set HG_MINGW=%HG_CCOMP%
   if "%HG_MINGW%" == "" set HG_MINGW=%HG_HRB%\comp\mingw
   if "%HG_CCOMP%" == "" set HG_CCOMP=%HG_MINGW%
   if "%LIB_GUI%"  == "" set LIB_GUI=lib\hb\mingw
   if "%LIB_HRB%"  == "" set LIB_HRB=lib\win\mingw
   if "%BIN_HRB%"  == "" set BIN_HRB=bin
   call "%HG_ROOT%\BuildApp_hbmk2.bat" %1 %2 %3 %4 %5 %6 %7 %8 %9
   goto END

:END
