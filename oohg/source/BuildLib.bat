@echo off
rem
rem $Id: BuildLib.bat $
rem

rem Use: BuildLib [f] [hbmk2 options]
rem f parameter redirectes all output to a file named error.log
rem hbmk2 usefull options:
rem -rebuild to force compiling of all sources
rem -trace to see the commands generated by hbmk2
rem -info to see informational messages

:BUILDLIB

   cls

   if /I not "%1" == "/C" goto ROOT
   shift
   set HG_ROOT=
   set HG_HRB=
   set HG_MINGW=
   set HG_BCC=
   set HG_CCOMP=
   set LIB_GUI=
   set LIB_HRB=
   set BIN_HRB=

:ROOT

   if not "%HG_ROOT%" == "" goto TEST
   set HG_ROOT=%~dp0..

:TEST

   if /I "%1" == "HB30" goto TEST_HB30
   if /I "%1" == "HB32" goto TEST_HB32

:DETECT_HB30

   if exist "%~dp0.\BuildLib30.bat" goto DETECT_HB32
   if exist "%~dp0.\BuildLib32.bat" goto COMPILE_HB32
   echo File %~dp0BuildLib30.bat not found !!!
   echo File %~dp0BuildLib32.bat not found !!!
   echo.
   echo This file must be executed from SOURCE folder !!!
   echo.
   goto END

:DETECT_HB32

   if not exist "%~dp0.\BuildLib32.bat" goto COMPILE_HB30
   echo Syntax:
   echo    To build with Harbour 3.0
   echo       buildlib HB30 [options]
   echo   To build with Harbour 3.2
   echo       buildlib HB32 [options]
   echo.
   goto END

:TEST_HB30

   shift
   if exist "%~dp0.\BuildLib30.bat" goto COMPILE_HB30
   echo File %~dp0BuildLib30.bat not found !!!
   echo.
   echo This file must be executed from SOURCE folder !!!
   echo.
   goto END

:TEST_HB32

   shift
   if exist "%~dp0.\BuildLib32.bat" goto COMPILE_HB32
   echo File %~dp0BuildLib32.bat not found !!!
   echo.
   echo This file must be executed from SOURCE folder !!!
   echo.
   goto END

:COMPILE_HB30

   if "%HG_HRB%   "== "" set HG_HRB=%HG_ROOT%\hb30
   if "%HG_MINGW%" == "" set HG_MINGW=%HG_CCOMP%
   if "%HG_MINGW%" == "" set HG_MINGW=%HG_HRB%\comp\mingw
   if "%HG_CCOMP%" == "" set HG_CCOMP=%HG_MINGW%
   if "%LIB_GUI%"  == "" set LIB_GUI=lib
   if "%LIB_HRB%"  == "" set LIB_HRB=lib
   if "%BIN_HRB%"  == "" set BIN_HRB=bin
   call "%~dp0.\BuildLib_hbmk2.bat" %1 %2 %3 %4 %5 %6 %7 %8 %9
   goto END

:COMPILE_HB32

   if "%HG_HRB%"   == "" set HG_HRB=%HG_ROOT%\hb32
   if "%HG_MINGW%" == "" set HG_MINGW=%HG_CCOMP%
   if "%HG_MINGW%" == "" set HG_MINGW=%HG_ROOT%\hb32\comp\mingw
   if "%HG_CCOMP%" == "" set HG_CCOMP=%HG_MINGW%
   if "%LIB_GUI%"  == "" set LIB_GUI=lib\hb\mingw
   if "%LIB_HRB%"  == "" set LIB_HRB=lib\win\mingw
   if "%BIN_HRB%"  == "" set BIN_HRB=bin
   call "%~dp0.\BuildLib_hbmk2.bat" %1 %2 %3 %4 %5 %6 %7 %8 %9
   goto END

:END
